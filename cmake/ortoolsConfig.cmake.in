## ortools CMake configuration file

set(@PACKAGE_PREFIX@_VERSION @PROJECT_VERSION@)

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
# Kitware CMake provide a FindZLIB.cmake module
if(NOT ZLIB_FOUND AND NOT TARGET ZLIB::ZLIB)
  find_dependency(ZLIB REQUIRED)
endif()

if(${CMAKE_VERSION} VERSION_GREATER_EQUAL "3.9.6")
  set(CONFIG_FLAG CONFIG)
endif()

if(NOT absl_FOUND)
  find_dependency(absl REQUIRED ${CONFIG_FLAG})
endif()

if(NOT Protobuf_FOUND AND NOT PROTOBUF_FOUND AND NOT TARGET protobuf::libprotobuf)
  find_dependency(Protobuf REQUIRED ${CONFIG_FLAG})
endif()

if(@BUILD_LP_PARSER@)
  if(NOT re2_FOUND AND NOT TARGET re2::re2)
    find_dependency(re2 REQUIRED ${CONFIG_FLAG})
  endif()
endif()

if(@USE_COINOR@)
  if(NOT Clp_FOUND AND NOT TARGET Coin::ClpSolver)
    find_dependency(Clp REQUIRED ${CONFIG_FLAG})
  endif()
  if(NOT Cbc_FOUND AND NOT TARGET Coin::CbcSolver)
    find_dependency(Cbc REQUIRED ${CONFIG_FLAG})
  endif()
endif()

if(@USE_GLPK@)
  if(NOT GLPK_FOUND AND NOT TARGET GLPK::GLPK)
    find_dependency(GLPK REQUIRED ${CONFIG_FLAG})
  endif()
endif()

if(@USE_SCIP@)
  if(NOT scip_FOUND AND NOT TARGET libscip)
    find_dependency(scip REQUIRED ${CONFIG_FLAG})
  endif()
endif()

if(@USE_XPRESS@)
  if(NOT XPRESS_FOUND AND NOT TARGET XPRESS::XPRESS)
    set(CMAKE_MODULE_PATH_OLD__ ${CMAKE_MODULE_PATH})
    unset(CMAKE_MODULE_PATH)
    list(APPEND CMAKE_MODULE_PATH
      ${PACKAGE_PREFIX_DIR}/lib/cmake/XPRESS
      ${PACKAGE_PREFIX_DIR}/lib64/cmake/XPRESS)
    find_dependency(XPRESS REQUIRED)
    set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH_OLD__})
  endif()
endif()

if(@USE_SIRIUS@)
  if(POLICY CMP0074)
	  cmake_policy(SET CMP0074 NEW)
	endif()
  if(NOT sirius_solver_FOUND AND NOT TARGET sirius_solver)
    find_dependency(sirius_solver REQUIRED ${CONFIG_FLAG})
  endif()
endif()

include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")
