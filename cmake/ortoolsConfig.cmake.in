## ortools CMake configuration file

set(@PACKAGE_PREFIX@_VERSION @PROJECT_VERSION@)

@PACKAGE_INIT@
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/modules")

include(CMakeFindDependencyMacro)
# Kitware CMake provide a FindZLIB.cmake module
if(NOT ZLIB_FOUND AND NOT TARGET ZLIB::ZLIB)
  find_dependency(ZLIB REQUIRED)
endif()

if(${CMAKE_VERSION} VERSION_GREATER_EQUAL "3.9.6")
  set(CONFIG_FLAG CONFIG)
endif()

if(NOT absl_FOUND)
  find_dependency(absl REQUIRED ${CONFIG_FLAG})
endif()

if(NOT Protobuf_FOUND AND NOT PROTOBUF_FOUND AND NOT TARGET protobuf::libprotobuf)
  # CMake provide a FindProtobuf module so we don't want to use the CONFIG_FLAG
  # also most distro still use the autotools based build for Protobuf.
  # ref: https://cmake.org/cmake/help/latest/module/FindProtobuf.html
  if(@BUILD_Protobuf@)
    find_dependency(Protobuf CONFIG REQUIRED)
  else()
    find_dependency(Protobuf REQUIRED)
  endif()
endif()

if(@BUILD_LP_PARSER@)
  # re2 may not provide a CMake config files
  if(NOT re2_FOUND AND NOT TARGET re2::re2)
    find_dependency(re2 REQUIRED)
  endif()
endif()

if(@USE_COINOR@)
  # COIN-OR packages don't provide CMake config files
  if(NOT Clp_FOUND AND NOT TARGET Coin::ClpSolver)
    find_dependency(Clp REQUIRED)
  endif()
  if(NOT Cbc_FOUND AND NOT TARGET Coin::CbcSolver)
    find_dependency(Cbc REQUIRED)
  endif()
endif()

if(@USE_GLPK@)
  if(NOT GLPK_FOUND AND NOT TARGET GLPK::GLPK)
    find_dependency(GLPK REQUIRED)
  endif()
endif()

if(@USE_HIGHS@)
  if(NOT HIGHS_FOUND AND NOT TARGET HIGHS::HIGHS)
    find_dependency(HIGHS REQUIRED ${CONFIG_FLAG})
  endif()
endif()

if(@USE_PDLP@)
  if(NOT Eigen3_FOUND AND NOT TARGET Eigen3::Eigen)
    find_dependency(Eigen3 REQUIRED)
  endif()
endif()

if(@USE_SCIP@)
  if(NOT scip_FOUND AND NOT TARGET libscip)
    find_dependency(SCIP REQUIRED)
  endif()
endif()

if(@USE_SIRIUS@)
  if(POLICY CMP0074)
	  cmake_policy(SET CMP0074 NEW)
	endif()
  if(NOT sirius_solver_FOUND AND NOT TARGET sirius_solver)
    find_dependency(sirius_solver REQUIRED ${CONFIG_FLAG})
  endif()
endif()

include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Targets.cmake")
